apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.firebase.crashlytics'

apply from: "$rootDir/tools/config/detekt/detekt.gradle"

def apikeyPropertiesFile = rootProject.file("apikey.properties")
def apikeyProperties = new Properties()
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

def storeKeyPropertiesFile = rootProject.file("storekey.properties")
def storeKeyProperties = new Properties()
storeKeyProperties.load(new FileInputStream(storeKeyPropertiesFile))

android {
    compileSdkVersion 31
    buildToolsVersion VersionsBuild.buildTools

    signingConfigs {
        release {
            storeFile file(storeKeyProperties['FILE_KEY'])
            storePassword storeKeyProperties['STORE_KEY']
            keyAlias storeKeyProperties['ALIAS_KEY']
            keyPassword storeKeyProperties['STORE_KEY']
        }
    }

    defaultConfig {
        applicationId VersionsBuild.applicationId
        minSdkVersion VersionsBuild.minSdk
        targetSdkVersion VersionsBuild.targetSdk
        versionCode VersionsBuild.versionCode
        versionName VersionsBuild.versionName

        testInstrumentationRunner VersionsTest.marvelTest

        buildConfigField "String", "PUBLIC_KEY", apikeyProperties['PUBLIC_KEY']
        buildConfigField "String", "PRIVATE_KEY", apikeyProperties['PRIVATE_KEY']
        buildConfigField "String", "BASE_URL", '"https://gateway.marvel.com/v1/public/"'
    }

    buildTypes {
        debug{
            applicationIdSuffix '.debug'
            debuggable true
        }
        staging{
            initWith debug
            applicationIdSuffix '.staging'
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro',
            'proguard-rules-staging.pro'
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
        freeCompilerArgs += ["-Xopt-in=kotlin.RequiresOptIn"]
    }

    testOptions {
        animationsDisabled = true
    }

    viewBinding {
        enabled = true
    }
}

dependencies {
    implementation project(':core')

    // AndroidX
    implementation AppVersions.core
    implementation AppVersions.appcompat
    implementation AppVersions.constraint

    // Material design
    implementation AppVersions.material
    implementation AppVersions.legacy

    // Navigation
    implementation AppVersions.navFragment
    implementation AppVersions.navUi

    // ViewModel and LiveData
    implementation AppVersions.lifecycleCommon
    implementation AppVersions.lifecycleViewModel
    implementation AppVersions.lifecycleLivedata
    implementation AppVersions.lifecycleRuntime

    //Coroutines
    implementation AppVersions.coroutines

    //DataStore
    implementation AppVersions.dataStore

    //Lottie animation
    implementation AppVersions.lottie

    // Koin
    implementation AppVersions.koinCore
    implementation AppVersions.koinAndroid

    // Room
    implementation AppVersions.roomKtx
    implementation AppVersions.roomRunTime
    implementation AppVersions.roomPaging
    kapt AppVersions.roomCompiler

    // Paging3
    implementation AppVersions.paging

    // Glide
    implementation AppVersions.glide
    kapt AppVersions.glideCompiler

    // Shimmer
    implementation AppVersions.shimmer

    //Firebase
    implementation platform("com.google.firebase:firebase-bom:${VersionsApp.firebase}")
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-config-ktx"
    implementation "com.google.firebase:firebase-inappmessaging-display-ktx"
    implementation "com.google.firebase:firebase-core"

    // Tests
    testImplementation project(":testing")
    testImplementation TestVersions.roomTest
    testImplementation TestVersions.mockitoCore
    debugImplementation TestVersions.mockWeb
    debugImplementation TestVersions.fragmentTest
    androidTestImplementation TestVersions.espressoContrib
    androidTestImplementation TestVersions.espressoCore
    androidTestImplementation TestVersions.junit
    androidTestImplementation TestVersions.testCore
    androidTestImplementation TestVersions.testRunner
    androidTestImplementation TestVersions.testRules
    androidTestImplementation TestVersions.mock
    androidTestImplementation TestVersions.koinTest
    androidTestImplementation TestVersions.mockitoAndroid
}
apply plugin: 'com.google.gms.google-services'
